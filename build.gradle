plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "ddb1eb01af"
}

version "1.8"
group "dev.gaminggeek"
sourceCompatibility = targetCompatibility = 8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientRunArgs += "--tweakClass net.modcore.loader.ModCoreSetupTweaker"
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://repo.sk1er.club/repository/maven-public"
    }
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include("net.modcore:Loader:2.0.0")
    include("com.squareup.okhttp3:okhttp:4.5.0")

    implementation("com.google.code.gson:gson:2.2.4")
    implementation("net.modcore:ModCore:713-10809-SNAPSHOT") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "asm"
    }
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.include]
    archiveClassifier.set("")

    manifest.attributes(
        "TweakClass": "net.modcore.loader.ModCoreSetupTweaker",
        "TweakOrder": "0"
    )
}

reobfJar.dependsOn tasks.shadowJar

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version
        expand "mcversion": project.minecraft.version
    }
}